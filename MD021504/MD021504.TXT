      *=======================================================          00001000
       IDENTIFICATION                                DIVISION.          00002000
      *=======================================================          00003000
       PROGRAM-ID. MD021504.                                            00004000
      *=======================================================          00005000
      *=======================================================          00005100
      * AUTOR: LEONARDO DE SOUZA SILVA                                  00005200
      * EMPRESA: FOURSYS                                                00005300
      * ANALISTA: IVAN SANCHES                                          00005400
      * DATA: 12/07/2022                                                00005500
      *-------------------------------------------------------          00005600
      * OBJETIVO: PRAGRAMA QUE FAZ REWRITE NO VSAM E TER UM             00005712
      * ARQUIVO DE DADOS E ASSIM ATUALIZAR O VSAM 'UPDADE EM            00005812
      * MASSA'.                                                         00005912
      *-------------------------------------------------------          00006010
      *                       ATUALIZACOES                              00006110
      *-------------------------------------------------------          00006210
4SYS15* PROGRAMADOR: LEONARDO DE SOUZA SILVA                            00006311
.     * EMPRESA....: FOURSYS                                            00006410
.     * DATA.......: 12/07/2022                                         00006610
4SYS15* OBJETIVO...: INCLUIR OS BOOKS                                   00006710
      *-------------------------------------------------------          00006910
4SYS15* PROGRAMADOR: LEONARDO DE SOUZA SILVA                            00007013
.     * EMPRESA....: FOURSYS                                            00007113
.     * DATA.......: 12/07/2022                                         00007213
4SYS15* OBJETIVO...: INCLUIR O 'EXC1207'                                00007313
      *-------------------------------------------------------          00007413
4SYS15* PROGRAMADOR: LEONARDO DE SOUZA SILVA                            00007517
.     * EMPRESA....: FOURSYS                                            00007617
.     * DATA.......: 13/07/2022                                         00007717
.     * OBJETIVO...: INCLUIR O 'FILLER' NO INICIO E NO FINAL            00007817
4SYS15* DA WORKING.                                                     00007917
      *-------------------------------------------------------          00008017
      * ARQUIVOS:                                                       00008117
      * DDNAME             I/O           INCLUDE/BOOK                   00008217
      * EVSA0407           I-O           FR15WEV4                       00008317
      * MOV1207             I            FR15WMOV                       00008417
      * EXC1207             O                                           00008517
      *-------------------------------------------------------          00008617
      *=======================================================          00008717
       ENVIRONMENT                                   DIVISION.          00008817
      *=======================================================          00008917
      *=======================================================          00009017
       CONFIGURATION                                  SECTION.          00009117
      *=======================================================          00009217
       SPECIAL-NAMES.                                                   00009317
            DECIMAL-POINT  IS COMMA.                                    00009417
                                                                        00009517
        INPUT-OUTPUT                                  SECTION.          00009617
        FILE-CONTROL.                                                   00009717
            SELECT EVSA0407 ASSIGN TO EVSA0407                          00009817
               ORGANIZATION IS INDEXED                                  00009917
               ACCESS MODE  IS DYNAMIC                                  00010017
               RECORD KEY   IS FD-CHAVE                                 00010117
               FILE STATUS  IS FS-EVSA0407.                             00010217
                                                                        00010317
            SELECT MOV1207 ASSIGN TO MOV1207                            00010417
                FILE STATUS IS FS-MOV1207.                              00010517
                                                                        00010617
4SYS15      SELECT EXC1207 ASSIGN TO EXC1207                            00010717
4SYS15          FILE STATUS IS FS-EXC1207.                              00010817
      *=======================================================          00010917
       DATA                                          DIVISION.          00011017
      *=======================================================          00011117
      *=======================================================          00011217
       FILE                                           SECTION.          00011317
      *=======================================================          00011417
       FD EVSA0407.                                                     00011517
4SYS15 COPY FR15WEV4.                                                   00011617
      *-------------------------------------------------------          00011717
       FD MOV1207                                                       00011817
           RECORDING MODE IS F.                                         00011917
4SYS15 COPY FR15WMOV.                                                   00012017
      *-------------------------------------------------------          00012117
4SYS15 FD EXC1207                                                       00012217
.          RECORDING MODE IS F.                                         00012317
4SYS15 01 FD-EXC1207                        PIC X(19).                  00012417
      *=======================================================          00012517
       WORKING-STORAGE SECTION.                                         00012617
      *=======================================================          00012717
4SYS15*-------------------------------------------------------          00012817
.      01 FILLER                            PIC X(70) VALUE             00012917
.               '----MD021504 - INICIO DA AREA DE WORKING----'.         00013017
4SYS15*-------------------------------------------------------          00013117
       77 WRK-LEN                           PIC 9999.                   00013217
      *-------------------------------------------------------          00013317
      *-------------------------------------------------------          00013417
       01 FILLER                            PIC X(70) VALUE             00013517
                '----VARIAVEIS PARA FILE STATUS----'.                   00013617
      *-------------------------------------------------------          00013717
       77 FS-EVSA0407                       PIC 9(02).                  00013817
       77 FS-MOV1207                        PIC 9(02).                  00013917
       77 FS-EXC1207                        PIC 9(02).                  00014017
      *-------------------------------------------------------          00014117
       77 WRK-SAIDA-REGISTROS               PIC X(19).                  00014217
      *-------------------------------------------------------          00014317
      *-------------------------------------------------------          00014417
       01 FILLER                            PIC X(70) VALUE             00014517
                '----VARIAVEIS DE ESPELHO----'.                         00014617
      *-------------------------------------------------------          00014717
       01 WRK-EVSA0407.                                                 00014817
          05 WRK-CHAVE.                                                 00014917
             10 WRK-AGENCIA                 PIC X(04).                  00015017
             10 WRK-CONTA                   PIC X(05).                  00015117
          05 WRK-SALARIO                    PIC X(10).                  00015217
      *-------------------------------------------------------          00015317
       01 FILLER                            PIC X(70) VALUE             00015417
                '----VARIAVEIS PARA MENSAGEMS----'.                     00015517
      *-------------------------------------------------------          00015617
       77 WRK-MSG-ERRO-EVSA0407             PIC X(25) VALUE             00015717
                      '--ERRO DE OPEN EVSA0407--'.                      00015817
       77 WRK-MSG-ERRO-MOV1207              PIC X(24) VALUE             00015917
                      '--ERRO DE OPEN MOV1207--'.                       00016017
       77 WRK-MSG-ERRO-EXC1207              PIC X(24) VALUE             00016117
                      '--ERRO DE OPEN EXC1207--'.                       00016217
       77 WRK-ERRO-CHAVE                    PIC X(21) VALUE             00016317
                      '--CHAVE INEXISTENTE--'.                          00016417
       77 WRK-SEM-REGISTRO                  PIC X(21) VALUE             00016517
                      '--SEM REGISTRO--'.                               00016617
       77 WRK-ATUALIZOU                     PIC X(21) VALUE             00016717
                      '--ATUALIZOU--'.                                  00016817
       77 WRK-NAO-ENCONTRADO                PIC X(27) VALUE             00016917
                      '--REGISTRO NAO ENCONTRADO--'.                    00017017
      *-------------------------------------------------------          00017117
       01 FILLER                            PIC X(70) VALUE             00017217
                '----VARIAVEIS ACUMULADORAS----'.                       00017317
      *-------------------------------------------------------          00017417
       77 WRK-ACUM-LIDOS                   PIC 9(02).                   00017517
       77 WRK-ACUM-REGRAVADOS              PIC 9(02).                   00017617
       77 WRK-ACUM-ERROS                   PIC 9(02).                   00017717
4SYS15*-------------------------------------------------------          00017817
.      01 FILLER                            PIC X(70) VALUE             00017917
.               '----MD021504 - FIM DA AREA DE WORKING----'.            00018017
4SYS15*-------------------------------------------------------          00018117
      *=======================================================          00018217
       PROCEDURE                                     DIVISION.          00018317
      *=======================================================          00018417
      *-------------------------------------------------------          00018517
       0000-PRINCIPAL                                 SECTION.          00018617
      *-------------------------------------------------------          00018717
            PERFORM 1000-INICIALIZAR.                                   00018817
             PERFORM 1200-TESTAR-VAZIO.                                 00018917
            PERFORM 2000-PROCESSAR UNTIL FS-MOV1207 EQUAL 10.           00019017
            PERFORM 3000-FINALIZAR.                                     00019117
            GOBACK.                                                     00019217
      *-------------------------------------------------------          00019317
       0000-99-FIM.                                      EXIT.          00019417
      *-------------------------------------------------------          00019517
      *-------------------------------------------------------          00019617
       1000-INICIALIZAR                               SECTION.          00020000
      *-------------------------------------------------------          00030000
            OPEN I-O    EVSA0407.                                       00040013
            OPEN INPUT  MOV1207.                                        00041013
            OPEN OUTPUT EXC1207.                                        00042015
             PERFORM 1100-TESTAR-STATUS.                                00043000
      *-------------------------------------------------------          00044000
       1000-99-FIM.                                      EXIT.          00045000
      *-------------------------------------------------------          00046000
      *-------------------------------------------------------          00047000
       1100-TESTAR-STATUS                             SECTION.          00047100
      *-------------------------------------------------------          00047200
             IF FS-EVSA0407 NOT EQUAL ZEROS                             00047300
              DISPLAY WRK-MSG-ERRO-EVSA0407 FS-EVSA0407                 00047403
              GOBACK                                                    00047500
             END-IF.                                                    00047600
      *-------------------------------------------------------          00047713
             IF FS-MOV1207  NOT EQUAL ZEROS                             00047803
              DISPLAY WRK-MSG-ERRO-MOV1207 FS-MOV1207                   00047903
              GOBACK                                                    00048003
             END-IF.                                                    00048103
      *-------------------------------------------------------          00048213
             IF FS-EXC1207  NOT EQUAL ZEROS OR FS-EXC1207 NOT NUMERIC   00048319
              DISPLAY WRK-MSG-ERRO-EXC1207 FS-EXC1207                   00048413
              GOBACK                                                    00048513
             END-IF.                                                    00048613
      *-------------------------------------------------------          00048700
       1100-99-FIM.                                      EXIT.          00048800
      *-------------------------------------------------------          00048900
      *-------------------------------------------------------          00049003
       1200-TESTAR-VAZIO                              SECTION.          00049103
      *-------------------------------------------------------          00049203
            READ MOV1207                                                00049303
             IF FS-MOV1207 NOT EQUAL ZEROS                              00049403
              DISPLAY WRK-SEM-REGISTRO                                  00049503
             END-IF.                                                    00049604
      *-------------------------------------------------------          00049703
       1200-99-FIM.                                      EXIT.          00049803
      *-------------------------------------------------------          00049903
      *-------------------------------------------------------          00050000
       2000-PROCESSAR                                 SECTION.          00050100
      *-------------------------------------------------------          00050200
            MOVE FD-MOV1207-CHAVE TO FD-CHAVE.                          00050303
4SYS15        MOVE FD-MOV1207 TO WRK-EVSA0407                           00050416
            READ EVSA0407                                               00050503
            ADD 1 TO WRK-ACUM-LIDOS                                     00050606
             IF FS-EVSA0407 EQUAL ZEROS                                 00050720
               REWRITE FD-EVSA0407 FROM WRK-EVSA0407                    00051003
                IF FS-EVSA0407 EQUAL ZEROS                              00051103
                 DISPLAY WRK-ATUALIZOU WRK-EVSA0407                     00051203
                 ADD 1 TO WRK-ACUM-REGRAVADOS                           00051305
                END-IF                                                  00051403
             ELSE                                                       00051625
4SYS15        MOVE WRK-EVSA0407 TO FD-EXC1207                           00052121
4SYS15         WRITE FD-EXC1207 FROM WRK-EVSA0407                       00052321
             END-IF.                                                    00052825
            READ MOV1207.                                               00052921
      *-------------------------------------------------------          00053021
       2000-99-FIM.                                      EXIT.          00053121
      *-------------------------------------------------------          00054000
      *-------------------------------------------------------          00057000
       3000-FINALIZAR                                 SECTION.          00057100
      *-------------------------------------------------------          00057200
            CLOSE EVSA0407                                              00057300
            CLOSE MOV1207                                               00057401
            CLOSE EXC1207                                               00057513
             IF FS-EVSA0407 NOT EQUAL ZEROS                             00057600
              DISPLAY 'ERRO AO FECHAR' FS-EVSA0407                      00057700
             END-IF.                                                    00057800
            DISPLAY ' '.                                                00057905
            DISPLAY 'LIDOS......:' WRK-ACUM-LIDOS.                      00058007
      *-------------------------------------------------------          00058100
            DISPLAY ' '.                                                00058205
            DISPLAY 'REGRAVADOS.:' WRK-ACUM-REGRAVADOS.                 00058305
      *-------------------------------------------------------          00058700
       3000-99-FIM.                                      EXIT.          00058800
      *-------------------------------------------------------          00058900
